apply plugin: 'com.android.library'

def readVersionCode() {
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', '--first-parent', '--count', 'master'
            standardOutput = code
        }
        return code.toString().trim().toInteger()
    } catch (ignored) {
        return 1
    }
}

def readVersionName() {
    try {
        def code = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'describe', '--tags', '--dirty', '--abbrev=7'
            standardOutput = code
        }
        return code.toString().trim()
    } catch (ignored) {
        return '0.1.0'
    }
}

android {
    compileSdkVersion 21
    buildToolsVersion '21.1.1'

    defaultConfig {
        applicationId 'com.bhatworks.android.slidingtab'
        minSdkVersion 14
        targetSdkVersion 21
        versionCode readVersionCode()
        versionName readVersionName()
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:21.0.2'
}
